# Firmware Updates

## Introduction

Nerves comes with a tool for handling firmware updates called
[fwup](https://github.com/fhunleth/fwup). It allows for archival and
application of firmware updates, however it does not handle distribution of
updates.  This Livebook comes with a module that can fetch a firmware update
from [Github releases](https://github.com/fhunleth/nerves_livebook/releases).

## Walk through

Evaluate the following to see what firmware version you're running:

```elixir
IO.write("""
You're running Nerves Livebook #{NervesLivebook.version()}.

More details:
Target: #{NervesLivebook.target()}
Firmware UUID: #{Nerves.Runtime.KV.get_active("nerves_fw_uuid")}
Firmware partition: #{Nerves.Runtime.KV.get("nerves_fw_active")}
""")
```

Nerves Livebook firmware images are hosted on GitHub under the [releases
tab](https://github.com/fhunleth/nerves_livebook/releases). Let's query GitHub
to see what the latest release is:

```elixir
NervesLivebook.check_internet!()

alias NervesLivebook.GithubRelease

{:ok, latest} = GithubRelease.get_latest("fhunleth/nerves_livebook")
IO.puts("The latest release is version #{GithubRelease.version(latest)}.")
```

Still interested in updating? It's ok to update to the same release if you just
want to try this out.

The next step is to download the firmware. This might take some time if you're
on a slow connection. Watch the evaluating circle pulse to know that it's working.

```elixir
{:ok, firmware_url} =
  GithubRelease.firmware_url(latest, "nerves_livebook", NervesLivebook.target())

IO.puts("Downloading #{firmware_url}...")

firmware_path = "/data/update.fw"
File.rm_rf!(firmware_path)

{:ok, :saved_to_file} =
  :httpc.request(:get, {firmware_url, []}, [], stream: to_charlist(firmware_path))
```

The firmware is now stored locally in `"/data/update.fw"`. We're ready to
install it, but first, let's check out its metadata and make sure it's valid:

```elixir
{output, 0} = System.cmd("fwup", ["--metadata", "-i", firmware_path])
IO.puts("Firmware metadata:\n" <> output)

# Validation isn't needed since the upgrade step also validates, but it's
# informative.
IO.puts("Validating archive...")
{output, _} = System.cmd("fwup", ["--verify", "-i", firmware_path])
IO.write(output)
```

The UUID metadata field is important. The UUID is computed from the contents
of the firmware archive and uniquely identifies what's running. If you're
ever unsure if you're running the exact same bits that you verified in QA,
check the UUID.

Ok, now we're ready for the big step. This won't erase any of your livebooks or
settings. It just updates the Nerves Livebook firmware. After this completes
successfully, your device will reboot.

```elixir
NervesLivebook.Fwup.upgrade(firmware_path)
```

## TODO: Other ways of updating firmware

Fill in `ssh` updates and mention NervesHub. Should we sign the firmware
updates on CI? That would be cool to show as well.
